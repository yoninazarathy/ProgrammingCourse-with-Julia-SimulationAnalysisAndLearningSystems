# Unit 4: More language features for software architecture

In the previous three units we explored basics of programming and computation (Unit 1), algorithms and data structures (Unit 2), and data files and numerics (Unit 3). In this unit we take a deeper and more thorough approach at basic Julia language features. 

Programming languages, including Julia, are designed mostly with these aims in mind:

 1. **Execution speed** — programs should run fast enough to solve the problem at hand. 
 2. **Coding speed** — writing programs to solve problems should be quick & easy for the coder.
 3. **Scalable engineering** — creating and maintaining large programs with many contributors should be no harder than writing small programs yourself.

Different programming languages have different goals and target audiences, and make different trade-offs and affordances for the above. The trickiest one to get right is the third one — as codebases grow it is _typical_ to get stuck in a tarpit of complexity, where it gets harder and harder to change and improve the code, and progress grinds to a halt. A corollary of that is that code in large pieces of software is _read_ far more often than it is _written_, so it becomes crucial to make code that is written easy to understand.

Julia is a high-performance language for solving large mathematical problems, and compiled code needs to run "close to the metal". It makes many affordances to solve mathematical problems quickly and efficiently. But most crucially, it tries to solve these in such a way that is "scalable" — where generic code can be reused and repurposed, and pieces of a program can be connected together elegantly like lego bricks.

The goal of this unit is to show you the most important language features of Julia and how to use them to best effect. The most important language features that we explore are the syntax, type system, user-defined types, and multiple-dispatch. At the end we'll consider how to use these to construct your code. The notes below often refer to the Julia documentation.

## Syntax

Before moving to types and dispatch, we'll cover some fundaments of Julia code syntax so we understand _what_ we are looking at later. A lot of this might be intuitive to some of you, but since we are mathematicians, we'd like to spell it out more formally :)

### Everything is an expression, and everything has a value

An expression is a piece of code that, when evaluated, returns a value. For example:

```julia
1 + 1
```

In Julia, _every_ piece of syntactically valid code is an expression — which means at run time it generates a value which you can assign to a variable. Even things like `if` statements!

```julia
x = 42

is_x_even = if x % 2 == 0
   "$x is even"
else
   "$x is odd"
end
```

Like many languages, Julia has a shorthand "ternary operator" (i.e. operator taking 3 inputs) for _if-else_ using `?` and `:`

```julia
is_x_even = x % 2 == 0 ? "$x is even" : "$x is odd"
```

It's worth noting these are 100% identical. We say that `x ? y : z` is "syntax sugar" for the `if` statement — it's a sweetener to make the programmer's life easier, but not an entirely new feature since you can always achieve the same thing with more characters of code. Another piece of syntax sugar is that `x + y` has exactly the same meaning as `+(x, y)`.

### Blocks of code

Above, we saw blocks of code containing values. They didn't really _do_ anything, like assign values to variables.

The rule in Julia is that the last expression in a block of code is the value returned from that block of code. If for some reason you want to create a code block (sometimes useful) you can use `begin` like so:

```julia
begin
    1
    2
    3
end
```

We see blocks of code everywhere in Julia — inside `if` and `else` statements, within `for` and `while` loops, within `function` bodies, etc. Blocks of code can be nested — we can have `if` statements inside `while` loops inside `function` bodies, and we tend to represent this visually with indentation:

```julia
function f(x)
    x = 0
    while x < 10
        if x % 2 == 0
            println("$x is even")
        else
            println("$x is odd")
        end
        x = x + 1
    end
    return x
end
```

_Note: `for` and `while` loops always return `nothing`!_

If you prefer, you can seperate multiple expressions in a code block with `;` instead of on seperate lines. Sometimes you'd even use parentheses to clarify where the code block begins and ends.

```julia
x = (1; 2; 3)
```

### Empty blocks of code return `nothing`

So what happens when the block of code is empty?

```julia
begin
end
```

Since every expression has a value in Julia, it must return _something_! That something is called `nothing`. It is a special builtin value that contains 0 bytes of data and has the builtin type `Nothing`.

```julia
typeof(nothing)
```

While the above is contrived, there are some more likely places to see this.

```julia
half_of_x = if x % 2 == 0
    x ÷ 2
end
```

This is _implicitly_ the same as

```julia
half_of_x = if x % 2 == 0
    x ÷ 2
else
end
```

which is _implicitly_ the same as

```julia
half_of_x = if x % 2 == 0
    x ÷ 2
else
    nothing
end
```

So if `x` is odd, then `half_of_x` is `nothing`.

We also use `nothing` as the return value for functions that don't have a value to return — e.g. when the purpose of the function is to perform some action rather that compute some value.

```julia
function hello()
    println("Hello class")
    return nothing
end

x = hello()
@show x;
```

Q: Does anyone know what the equivalent thing in _C_ is?

### Why expressions? Metaprogramming!

Having the syntax rule that "all valid syntax is an expression" makes it easier to analyse and manipulate code than it would be otherwise. It lets you rearrange code much like you can manipulate a mathematical expression with algebra (via substitution, etc), resulting in code that is correct and still compiles.

For human coders, this means it is easier to copy-paste code from one place to another, or refactor it into a function.

A "metaprogram" is a program that writes a program. Julia supports metaprogramming through macros and other advanced techniques. We won't be teaching how to create a macro, but using a macro is pretty easy. A good example is the `@show` macro:

```julia
x = 10
@show x + 1
```

Effectively the macro takes the "expression" `x + 1`, prints the expression, prints equals, and prints the evaluated value. While Julia is busy replacing the builtin syntax sugar (like operators, `x + 1`) with more fundamental expressions (like function calls, `+(x, 1)`), it also "expands" the macro, resulting in code that is roughly:

```julia
x = 10
println("x + 1 = ", x + 1)
```

Each macro can be thought of as like user-defined syntax sugar. We can extend the language with our own ideas of how we can write programs.

### An even better way to metaprogram

Another form of metaprogramming in Julia is multiple dispatch. Multiple dispatch allows us to construct entire classes of programs that depend on the _types_ of values — and Julia will construct on-demand a particular program _specialized_ on the inputs you actually provide. Next we'll learn more about Julia functions, Julia types and how multiple dispatch works.

## Functions in Julia

Functions are at the heart of Julia, and there's lots of ways of expressing different functions. The [Julia documentation of functions](https://docs.julialang.org/en/v1/manual/functions/) provides a rich description of all of the details. We now overview a few special features that were perhaps not evident from Units 1-3. There are also links to the documentation for it.

### [Function syntaxes](https://docs.julialang.org/en/v1/manual/functions/#man-functions)

In Julia there are multiple ways of creating a function. There is the "long form":

```julia
function f1(x)
    return x^2
end
```

The `return` is optional in this case — why?

There is a "short form":

```julia
f2(x) = x^2
```

And there are "anonymous functions" (or "arrow functions"):
```julia
const f3 = x -> x^2
```

In this form `x -> x^2` is an expression that constructs a function which is "anonymous" — it has no name. We have bound it to the variable `f3` so we can use it later. You can even do this in "long form"!

```julia
const f4 = function (x)
    return x^2
end
```

(Note: the `const` isn't essential — but it will let us attach other methods to `f3` or `f4` later)

### Nested functions: "closures"

In Julia you can define functions within functions.

```julia
function adder(x)
    return y -> y + x
end
```

This function returns another function! We can use the output to add things.

```julia
add_2 = adder(2)
add_2(3)
```

Such functions are called "closures" because they "close over" (or "capture") the values of variables outside their scope — in this case it captured the value of `x`, `2`, and implicitly stores it inside `add_2`.

```julia
add_2.x
```

Note again it doesn't matter which form of function syntax we use. It's just different syntax for the same thing. We could have equally written:

```julia
function adder(x)
    return function (y)
        return y + x
    end
end
```

In Julia, closures are given their own types and methods. The closure is just an automatically generated `struct` datatype with a method attached.

```julia
typeof(add_2)
```

```julia
methods(add_2)
```

Because closures are "just" a convenient way to do things you can already do in Julia with structs and methods, you can think of them as an advanced type of syntax sugar. The above is equivalent to this:

```julia
struct Adder{T}
    x::T
end

function (a::Adder)(y)
    return a.x + y
end

function adder(x)
    return Adder(x)
end

add_2 = adder(2)
```

```julia
add_2(3)
```

Note that _every_ function in Julia has its own type — e.g. `sin` and `cos` have different types. We'll come back to types and methods later.

### [Type assertions](https://docs.julialang.org/en/v1/manual/types/#Type-Declarations)

The `::` operator has two closely-related meanings.

First is type assertion. In programming an "assertion" is something the programmer asserts to be true, which either the compiler or runtime will check.

```julia
42::Int
```

You could read the above as "`42` must be an `Int`". The result is just the value — `::` doesn't normally do _anything_, unless the assertion is false

```julia
42::String
```

Julia has an "abstract" `Number` type.

```julia
42::Number
```

This is fine since `Int` is a subtype of `Number`, which itself could be written:

```julia
Int <: Number
```

(Note that this one returns `true` or `false`.)

The type `Any` is the supertype of every type.

```julia
42::Any  # always correct!
```

This type assertion always checks out, and doesn't really have any affect on your program. Therefore `x` and `x::Any` are the same thing. Such code is said to be a "no-op" because it corresponds to "no operation".

### [Argument types](https://docs.julialang.org/en/v1/manual/functions/#Argument-type-declarations)

The second meaning of `::` is in function / method signatures, which constrains the types of arguments the method will accept.

We have seen Julia methods from the start, e.g.

```julia
f(x::Int) = x^2
```

or,

```julia
f(x::Number) = x^2
```

or simply,

```julia
f(x) = x^2
```

Remember that `x` and `x::Any` are the same thing. The final method will accept _any_ inputs. Whether or not `x^2` works or throws an error depends on `x`.

In a sense, the third method isn't _safe_ since someone could provide an input like `(1, 2, 3)` that can't be squared. What happens if someone provides a string? Is this expected? On the other hand, the third method provides maximum flexibility — someone can introduce a new type that works with `f` at any point in time, without having to edit the definition of `f` to make it work.

### [Varargs functions](https://docs.julialang.org/en/v1/manual/functions/#Varargs-Functions)

Sometimes functions have a variable number of arguments — sometimes called "varargs" (or "variadic functions").

A simple thing to do might be to add up all the inputs:

```julia
function add_all(inputs...)
    out = 0
    for x in inputs
        out += x
    end
    return out
end

add_all(1, 2, 3, 4, 5)
```

```julia
function polynomialGenerator(a...)
    n = length(a) - 1
    poly = function(x)
        return sum([a[i+1]*x^i for i in 0:n])
    end
    return poly
end

polynomial = polynomialGenerator(1, 3, -10)

[polynomial(-1), polynomial(0), polynomial(1)]
```

You could use then use `Roots` package to automatically find out where all the inputs produce zero output:

```julia
using Roots

zero_vals = find_zeros(polynomial, -10, 10)
println("Zeros of the function f(x): ", zero_vals)
```

Here you start to see how Julia features like varargs functions, closures, and packages let you solve a wide range of problems without writing very much code.

### [Optional arguments](https://docs.julialang.org/en/v1/manual/functions/#Optional-Arguments)

You can have optional arguments (or default values):

```julia
using Distributions

function my_density(x::Float64, μ::Float64 = 0.0, σ::Float64 = 1.0)
    return exp(-(x-μ)^2 / (2σ^2)) / (σ*√(2π))
end

x = 1.5
@show pdf(Normal(), x), my_density(x)
@show pdf(Normal(0.5), x), my_density(x, 0.5);
```

### [Keyword arguments](https://docs.julialang.org/en/v1/manual/functions/#Keyword-Arguments)

Arguments following the `;` character in the function definition or the function call are called **keyword arguments**. These are named as they are used.

```julia
function my_density(x::Float64; μ::Float64 = 0.0, σ::Float64 = 1.0)
    return exp(-(x-μ)^2/(2σ^2) ) / (σ*√(2π)) 
end

@show pdf(Normal(0.0,2.5),x), my_density(x, σ=2.5);
```

```julia
function my_very_flexible_function(x::Number; kwargs...)
    println(kwargs)
    #....
end

my_very_flexible_function(2.5, a=1, b="two", c=:three)
```

The standard non-keyword arguments are called "positional" arguments since they are identified by their position not their name.

Note that when _calling_ a function you may either put a `;` or a `,` before the keyword arguments. In a function definition there's an ambiguity between optional arguments and keyword

### [Do Block Syntax for Function Arguments](https://docs.julialang.org/en/v1/manual/functions/#Do-Block-Syntax-for-Function-Arguments)

As you know, you can pass functions as arguments and sometimes you can use an anonymous function for that. For example,

```julia
using Random

Random.seed!(0)
data = rand(Int, 100)
filter(x -> (x % 10) == 0, data)
```

What if the anonymous function has more lines of code?

```julia
using Primes

filter(function (x)
    if !isprime(x % 100)
        return x < 30
    else
        return x > 50
    end
end, data)
```

This is a little ugly. It can be hard to see where the function starts and ends, find the second argument, etc.

In Julia there's a standard pattern that higher-order functions accept a function in their _first_ argument. There is a fancy bit of syntax sugar called `do` which lets you inject a function into the _first_ argument of a function call.

This looks like:

```julia
filter(data) do (x)
    if !isprime(x % 100)
        return x < 30
    else
        return x > 50
    end
end
```

### [Destructing](https://docs.julialang.org/en/v1/manual/functions/#Argument-destructuring)

In Julia you can _destructure_ tuples (and other iterables) in the reverse way you _construct_ them:

```julia
my_tuple = (42, "abc")  # construct
(a, b) = my_tuple       # destruct

@show a
@show b;
```

A really common pattern here is swapping two variables:

```julia
x = 1
y = 2
(x, y) = (y, x)
@show x
@show y;
```

You can also destructure fields of structs and named tuples in the reverse way you create a named tuple:

```julia
function f(x::Complex)
    (; re, im) = x
    println("Real part: $re")
    println("Imaginary part: $im")
end

f(10 + 42im)
```

One thing you can do is destructure arguments to functions as they come in. For example:

```julia
f((x,y)) = x + y
my_pair = (4, 5)
f(my_pair)
```


### [Function composition and piping](https://docs.julialang.org/en/v1/manual/functions/#Function-composition-and-piping)

Just a little bit of syntax sugar for "piping" values into function calls:

```julia
π/4 |> cos |> acos |> x -> 4x
```

Here is a function just like `identity`:

```julia
const ii = cos ∘ acos #\circ + [TAB]
(ii(π/4), π/4)
```

### [Dot syntax for broadcasting functions](https://docs.julialang.org/en/v1/manual/functions/#man-vectorized)

You already know the broadcast operator. It is syntax sugar for the `broadcast` function.

```julia
x_range = 0:0.5:π
cos.(x_range)
```

See the docs for a discussion of performance of broadcasting.

You can also use the macro `@.`

```julia
x_range = 0:0.5:π
@. cos(x_range + 2)^2
```

## More items from control flow

You are already very familiar with conditional statements (`if`, `elseif`, `else`), with loops (`for` and `while`), with short circuit evaluation, and with many other variants. For example you can use `continue` and `break` in loops, and Julia even supports `@goto` and `@label` for when that isn't enough.

You can find more control flow details here: [control flow in Julia docs](https://docs.julialang.org/en/v1/manual/control-flow/). We'll just dive into `for` loops and error handling below.

### Iteration and `for` loops

One impressive piece of syntax sugar is the `for` loop. It is in fact a special case of a `while` loop, like so:

```julia; eval = false
# this loop...
for x in iterable
    # <CODE>
end

# ...is equivalent to
tmp = iterate(iterable)
while tmp !== nothing
    (x, state) = tmp

    # <CODE>
    
    tmp = iterate(iterable, state)
end
```
In the above, `tmp` is either `nothing` or it's a tuple of length two — containing the next value and whatever metadata is needed to iterate to the next element.

You can make any of your data types iterable in a `for` loop by implementing a method on the `iterate` function. We won't be doing this in this course, but it is an excellent example of an _interface_ in Julia. We'll talk more about interfaces later.

### [Errors and exceptions](https://docs.julialang.org/en/v1/manual/control-flow/#Exception-Handling)

One additional thing to know about is exception handling.

```julia
function my_2_by_2_inv(A::Matrix{Float64})
    if size(A) != (2,2)
        error("This function only works for 2x2 matrices")
    end

    d = A[1,1]*A[2,2] - A[2,1]*A[1,2]

    if d ≈ 0
        throw(ArgumentError("matrix is singular or near singular")) #\approx + [TAB]
    end

    return [A[2,2] -A[1,2]; -A[2,1] A[1,1]]/d
end

my_2_by_2_inv(rand(3,3))
```

```julia
my_2_by_2_inv([ones(2) ones(2)])
```

```julia
using LinearAlgebra

Random.seed!(0)
A = rand(2, 2)
A_inv = my_2_by_2_inv(A)
@assert A_inv*A ≈ I
```

```julia
Random.seed!(0)
for _ ∈ 1:10 #\in + [TAB]
    A = float.(rand(1:5,2,2))
    try
        my_2_by_2_inv(A)
    catch e
        println(e)
    end
end
```

An exception may be caught way down the call stack:

```julia
A = ones(2,2)
f(mat) = 10*my_2_by_2_inv(A)
g(mat) = f(mat .+ 3)
h(mat) = 2g(mat)
h(A)
```
```
ERROR: ArgumentError: matrix is singular or near singular
Stacktrace:
 [1] my_2_by_2_inv(A::Matrix{Float64})
   @ Main ~/git/mine/ProgrammingCourse-with-Julia-SimulationAnalysisAndLearningSystems/markdown/lecture-unit-4.jmd:5
 [2] f(mat::Matrix{Float64})
   @ Main ./REPL[33]:1
 [3] g(mat::Matrix{Float64})
   @ Main ./REPL[34]:1
 [4] h(mat::Matrix{Float64})
   @ Main ./REPL[35]:1
 [5] top-level scope
   @ REPL[36]:1
```

```julia
try 
    h(A)
catch e
    println(e)
end
```

#### Try, catch and finally

It's possible to catch and handle errors that happened using `try`, `catch` and `finally`.

```julia
try
    0 ÷ 0
catch e
    @show e
finally
    println("done")
end;
```

#### Resource cleanup

One place closures, and `do` syntax in particular, gets used is when you want resources to be cleaned up automatically. Here's how one method of `open` is defined in Julia:

```julia; eval = false
function open(f, x)
    io = open(f)
    try    
        f(io)
    finally
        close(io)
    end
end
```

When you want to use this function it is common to use `do` syntax like so:

```julia; eval = false
open(file) do (io)
    # Read the first 128 bytes from the file
    return read(io, 128) # might throw an exception
end
```

This way even your code might error out, the file is _always_ closed in the `finally` block above. Using this pattern avoids mistakes and resource leakage.

### Variable scope

See [variables and scoping in Julia docs](https://docs.julialang.org/en/v1/manual/variables-and-scoping/).

Julia actually has two seperate scopes — global or "toplevel" scope, and local scope.

Local scope is the simplest. Variables inside a function body take precedence (or "shadows") any variables of the same name outside the function body.

```julia
x = 0

function f()
    x = 1
    return 2 * x
end

f()
```

It gets a little more complicated when things are outside.

```julia
data = [1, 2, 3]
s = 0
β, γ = 2, 1

for i in 1:length(data)
    global s    #This usage of the `global` keyword is not needed in Jupyter
                #But elsewhere without it:
                #ERROR: LoadError: UndefVarError: s not defined
    s += β*data[i]
    data[i] *= -1 #Note that we didn't need 'global' for data
end
#print(i)       #Would cause ERROR: LoadError: UndefVarError: i not defined
@show data
@show s

function sum_data(β)
    s = 0           #try adding the prefix global
    for i in 1:length(data)
        s += β*(data[i] + γ)
    end
    return s
end
@show sum_data(β/2)
@show s
```

Julia uses Lexical scoping:

```julia
function my_function()
    x = 10
    function my_function_inside_a_function()
        @show x
    end
    return my_function_inside_a_function
end

x = 20
f_ret = my_function()
f_ret();
```

The use of `outer`:

```julia
function f()
    i = 0
    for i = 1:3
        # empty
    end
    return i
end
f()
```

```julia
function f()
    i = 0
    for outer i = 1:3
        # empty
    end
    return i
end
f()
```

My advice:

 * use global variables as little as possible
 * use distinct names for variables as much as possilbe
 * the longer a varible hangs around, the longer and more discriptive it's name should be

Remember you want your programs to be _simple_ and _easy to read_. Variable names are cheap.

## Types and the Type System

See [types in the Julia docs](https://docs.julialang.org/en/v1/manual/types/).

### Everything has a type

```julia
typeof(2.3)
```

```julia
typeof(2.3f0)
```

```julia
typeof(2)
```

```julia
typeof(23 // 10)
```

```julia
typeof(2 + 3im)
```

```julia
typeof(2.0 + 3im)
```

```julia
typeof("Hello!")
```

```julia
typeof([1, 2, 3])
```

```julia
typeof([1, 2, 3.0])
```

```julia
typeof([1.0, 2, "three"])
```

```julia
typeof((1.0, 2, "three"))
```

```julia
typeof(1:3)
```

```julia
typeof([1 2; 3 4])
```

```julia
typeof(Float64)
```

```julia
typeof(:Hello)
```

## Concrete Types

Every value in Julia has what is called a "concrete" type. Julia also has "abstract" types (like `Any` and `Number`) that we'll discuss below. Only concrete types can have values - abstract types can have subtypes and *those* may be concrete.

### Primitive types

The most _basic_ type in Julia is the primitive type. You can create your own types with the `primitive type` keywords.

 Here's some definitions from `Base`:

```julia; eval = false
primitive type UInt8
    8
end

primitive type Int32
    32
end

primitive type Float64
    64
end
```

The number of bits must be a multiple of `8` (i.e. whole bytes).

By default, primitive types have no methods associated with them - you need to define low-level primitive actions for them (e.g. how to add integers). The one thing you can do is `reinterpret` one primtivie type as another with the same number of bits.

```julia
reinterpret(UInt64, 0.0)
```

```julia
reinterpret(UInt64, 1.0)
```

Primitive types are treated by Julia as _immutable_ — once a value is created, it cannot be modified or "mutated". You don't modify an `Int64` in Julia when you increment it like `i += 1`, this is syntax sugar for `i = i + 1`, where `i + 1` returns a new `Int64` and the variable `i` is bound to a new value. 

(As an optimization, compiled Julia code may actually modify some bit of RAM in-place, but this effect is not visible to the user in any way other than being fast.)

### Structs and mutable structs

Another concrete type in Julia is the `struct`. A simple example is a complex number with a real and complex part (as defined in `Base`):

```julia; eval = false
struct Complex{T <: Number} <: Number
    re::T
    im::T
end
```

Here we see a few features of `struct`. A struct — or structure — contains structured data within, having fields with names and types. We again use the type assertion operator `::` to indicate the type, which could be a primitive type, a struct, or an abstract type like `Any` or `Number`. And the type can be _parameterized_ — in this case it has an associated type `T` which must be a subtype of `Number`. Finally, we can define that `Complex` is a subtype of `Number`. We'll talk more about subtypes later.

Generally you _construct_ a struct value using a function call, like:

```julia
z = Complex{Int64}(2, 3)
```

Like primitive types, structs are by default immutable. You cannot modify an existing complex number — an operation like `z += 2im` returns a _new_ complex number via a constructor, e.g. `Complex{Int64}(2, 5)`. It is an error to do `z.im = 5`.

If you wanted to, you could create your own _mutable_ struct using the `mutable struct` keywords:

```julia
mutable struct MutableComplex{T <: Number}
    re::T
    im::T
end

z = MutableComplex{Int64}(2, 3)
z.im += 2
z
```

Note that we haven't yet defined any functions that act on our new `MutableComplex` type. If we wanted it to print like the built-in `Complex` numbers (like `2 + 5im`), we'd need to define a method for `Base.show`.

```julia
function Base.show(io::IO, z::MutableComplex)
    print(io, z.re)
    print(io, " + ")
    print(io, z.im)
    print(io, "im")
end

MutableComplex{Int64}(2, 3)
```

If we wanted it to add, subtract, multiply or divide we'd need to define methods of `+`, `-`, `*` and `/`. For example:

```julia
function Base.:+(z1::MutableComplex, z2::MutableComplex)
    return MutableComplex(z1.re + z2.re, z1.im + z2.im)
end

MutableComplex{Int64}(2, 3) + MutableComplex{Int64}(10, 6)
```

There are yet more functions to overload that would allow it to work well with other `Number` types (e.g. add a `MutableComplex{Float64}` and a real `Int64`). We could do that with brute force by defining methods for every possible combination — but we will see a better way when we study generic code and interfaces, later.

### `DataType`

Julia comes with a built-in struct called `DataType`. It has a pretty complex definition:

```julia; eval = false
mutable struct DataType <: Type
    name::TypeName
    super::Type
    parameters::Tuple
    names::Tuple
    types::Tuple
    ctor::Any
    instance::Any
    size::Int32
    abstract::Bool
    mutable::Bool
    pointerfree::Bool
end
```

It holds all sorts of details about concrete data types. Every concrete data type is a `DataType`:

```julia
@show typeof(Int64)
@show typeof(Complex{Float64})
@show typeof(Vector{String});
```

You can poke around in these structures to learn things:

```julia
@show Int64.size
@show Complex{Float64}.size
@show Complex{Float64}.name.names
@show Complex{Float64}.types;
```

Given that `DataType` is just a mutable struct, it's type is also a `DataType`:

```julia
@show typeof(DataType)
```

It's circular, but logically consistent. Note that all Turing-complete systems require some kind of circular or higher-order logic like this at some point.

### Built-in types

There are a handful of builtin types that are interesting in Julia.

#### `Array`

The (multidimensional) array is Julia's only built-in type of arbitrary size. A primitive type or struct consists of a known set of bits, while an `Array` can be dynamically sized at run time.

Arrays have two type parameters, `Array{T, N}`. Here `T` is the element type, and `N` is the dimensionality of the array.

There are a couple type aliases defined:

```julia; eval = false
const Vector{T} = Array{T, 1}
const Matrix{T} = Array{T, 2}
```

Note that `Vector` is resizable after it is created, but this isn't true for other dimensionalities.

In Julia, arbitrary bytes of data (like the contents of a file) is generally represented by `Vector{UInt8}`.

#### `Tuple`

A tuple is like an immutable struct where the field names are `1`, `2`, `3`, etc.

```julia
tuple = (42, 3.14, "abc")
```

When you call a function in Julia, `f(42, 3.14, "abc")`, it is a bit like the function takes a single tuple as its input.

Tuples have some special rules when it comes to _abstract_ types to help with multiple-dispatch, but that is a rather advanced topic. Despite that, every tuple value behaves just like plain old data.

#### `NamedTuple`

The `NamedTuple`, like `(a = 1, b = true, c = "abc")`, is a special built-in type. However, it behaves just like a regular struct with field names you can choose, and is immutable.

```julia
typeof((a = 1, b = true, c = "abc"))
```

## Abstract types

Julia has a type hierarchy (a tree). At the top of the tree is the type `Any`. All types have a supertype (the supertype of `Any` is `Any`). Types that are not leaves of the tree have subtypes. Some types are **abstract** while others are **concrete**. One particularly distinctive feature of Julia's type system is that concrete types may not subtype each other: all concrete types are final and may only have abstract types as their supertypes.  

```julia
x = 2.3
@show typeof(x)
@show supertype(Float64)
@show supertype(AbstractFloat)
@show supertype(Real)
@show supertype(Number)
@show supertype(Any);
```

There is an **is a** relationship:
```julia
isa(2.3, Number)
```

```julia
isa(2.3, String)
```

```julia
2.3 isa Float64
```

Note that `x isa T` is the same as `typeof(x) <: T`, where we say `<:` as "is a subtype of".

```julia
@show Float64 <: Number
@show String <: Number;
```

We can ask whether a given type is abstract or concrete.

```julia
@show isabstracttype(Float64)
@show isconcretetype(Float64);
```

```julia
@show isabstracttype(Real)
@show isconcretetype(Real);
```

Structs with undefined type paremeters are not concrete:

```julia
@show isconcretetype(Complex);
```

Once we provide the type parameters we do get a concrete type:

```julia
@show isconcretetype(Complex{Float64});
```

As mentioned, Julia has a type tree. Let's walk down from `Number`:

```julia
using InteractiveUtils: subtypes

function type_and_children(type, prefix = "", child_prefix = "")
    if isconcretetype(type)
        @assert isempty(subtypes(type))

        println(prefix, type, ": concrete")
    else
        println(prefix, type, isabstracttype(type) ? ": abstract" : ": parameterized")

        children = subtypes(type)
        for (i, c) in enumerate(children)
            if i == length(children)
                type_and_children(c, "$(child_prefix) └─╴", "$(child_prefix)    ")
            else
                type_and_children(c, "$(child_prefix) ├─╴", "$(child_prefix) │  ")
            end 
        end
    end
end

type_and_children(Number)
```

In Julia, you can define abstract types with the `abstract type` keywords:

```julia; eval = false
abstract type Number
end

abstract type Real <: Number
end

abstract type AbstractFloat <: Real
end

primitive type Float64 <: AbstractFloat
    64
end
```


A type can be **mutable** or not (**immutable**). Variables/data of the immutable types are typically stored on the stack. Variables/data of mutable types are typically allocated and stored on the heap. 

```julia
x = 7
@show ismutable(x)

x = [7]
@show ismutable(x);
```

When you pass a mutable variable to a function, the function can change the value. When you pass an immutable variable, if you try to change it, Julia will just make another instance.

```julia
f(z::Int) = begin z = 0 end
f(z::Array{Int}) = begin z[1] = 0 end

x = 1
@show typeof(x)
@show isimmutable(x)
println("Before call by value: ", x)
f(x)
println("After call by value: ", x,"\n")

x = [1]
@show typeof(x)
@show isimmutable(x)
println("Before call by reference: ", x)
f(x)
println("After call by reference: ", x)
```

Making copies, `copy` and `deepcopy`:

```julia
println("Immutable:")
a = 10
b = a
b = 20
@show a;
```

```julia
println("\nNo copy:")
a = [10]
b = a
b[1] = 20
@show a;
```

```julia
println("\nCopy:")
a = [10]
b = copy(a)
b[1] = 20
@show a;
```

```julia
println("\nShallow copy:")
a = [[10]]
b = copy(a)
b[1][1] = 20
@show a;
```

```julia
println("\nDeep copy:")
a = [[10]]
b = deepcopy(a)
b[1][1] = 20
@show a;
```


## Defining your Own Types

You can define your own types. Any "serious" programming task would almost always merit that you do that.

In object oriented languages (e.g. C++, Java, Python) types are typically called classes. A class (in such a language) will have both definitions of data and actions, typically called variables and methods respectively. An instance of a class would be called an object. 

Julia is not object oriented. It rather provides a different paradigm based on multiple-dispatch (which we describe below). Nevertheless, there are user defined types, called structs (structures). The name comes from C. 

```julia
struct Person #Notice the convention of using capital letters for the first letter of a struct
    height::Float64
    weight::Float64
    name::String
end

person = Person(179.8, 78.6, "Miriam") #A struct comes with a constructor function

@show typeof(person)

@show person.height; #The fields of a struct are accessed via "." - not to be confused with "." used for broadcasting.
@show person.weight;
@show person.name;
```

```julia
@show ismutable(person)
person.weight = 85.4 #gained some weight - but this will generate and error
```

Here is a `mutable struct`

```julia
mutable struct MutablePerson
    height::Float64
    weight::Float64
    name::String
end

person = MutablePerson(179.8, 78.6, "Miriam")
person.weight = 85.4
println(person)
```

**Note:** You typically cannot redefine a type during the same Julia session. One workaround for that is to use the [Revise.jl](https://github.com/timholy/Revise.jl) package. We won't use it just yet.

```julia
struct MyStruct
    x::Int
end

struct MyStruct #Will generate an ERROR because redefining a struct
    x::Int
    y::Float64
end
```
You can define abstract types (they won't have any fields) and a whole type hierarchy:

```julia
abstract type Animal end

abstract type Mammal <: Animal end
abstract type Reptile <: Animal end

struct Human <: Mammal
    height::Float64
    weight::Float64
    religious::Bool
end    

struct Dog <: Mammal
    height::Float64
    weight::Float64
end

struct FlexDog{T <: Real} <: Mammal
    height::T
    weight::T
end

struct Crocodile <: Reptile
    length::Float64
    weight::Float64
    type::Symbol #Expect to be :salt_water or :fresh_water
end

type_and_children(Animal)
```

As stated above, the function that creates an instance of the type is called the **constructor**. Every concrete type comes with a default constructor.

```julia
methods(Crocodile)
```

```julia
tick_tock = Crocodile(2.3, 204,:salt_water)
```

You can also create other constructor methods:

```julia
function Crocodile(type::Symbol)
    if type == :salt_water
        return Crocodile(2.0,200,:salt_water) #average salt water croc
    elseif type == :fresh_water
        return Crocodile(1.5,150,:fresh_water) #average fresh water croc
    else
        error("Can't make crocodile of type $type")
    end
end

methods(Crocodile)
```

```julia
Crocodile(:salt_water) 
```

```julia
Crocodile(:fresh_water) 
```

```julia
Crocodile(:ice_water) #will generate an error
```

A bit more on [constructors](https://docs.julialang.org/en/v1/manual/constructors/) will come later.

Notice we had the [parameteric type](https://docs.julialang.org/en/v1/manual/types/#Parametric-Types) `FlexDog`:

```julia
dash_my_dog = FlexDog(2,4)
@show typeof(dash_my_dog)

lassy_your_dog = FlexDog(2.3f0,5.7f0)
@show typeof(lassy_your_dog)

my_dog_array = FlexDog{UInt16}[]
```

```julia
my_dog_array = FlexDog{Complex}[] #Will not work because Complex is not a Real
```

With multiple dispatch we can get a form of polymorphism:

```julia
animal_noise(animal::Dog) = println("woof")
animal_noise(animal::Human) = println("hello")
animal_noise(animal::Crocodile) = println("chchch")

animals = [Crocodile(:salt_water), Human(2.4,2.3,false), Crocodile(:salt_water), Dog(5.3,2.5)]
animal_noise.(animals);
```

```julia
methods(animal_noise)
```

We can even handle the `FlexDog`:

```julia
animal_noise(animal::Union{Dog,FlexDog}) = println("woof")
push!(animals, FlexDog{Int16}(2,4))
animal_noise.(animals);
```

### Methods and Multiple Dispatch

[See methods in Julia docs](https://docs.julialang.org/en/v1/manual/methods/).

If there is one key attribute to Julia it is **multiple dispatch** as we have just seen above. 

```julia
function my_f(x::Int)
    println("My integer is $x")
end

function my_f(x::Float64)
    println("My floating point number is $x")
end

my_f(2)
my_f(2.5)
println(methods(my_f))
```

It is worthwhile to watch this [video](https://www.youtube.com/watch?v=kc9HwsxE1OY) about the philosphy of multiple dispatch. Some of the content of the video may be a bit advanced, but then towards the end of the course it is worth to listen to it again and see what makes sense and what not yet.

### Defining more methods for existing functions

Almost any operation in julia is a function. For example,

```julia
@which 2+3
```

There are many methods for `+`:

```julia
methods(+) |> length
```

So what if we had our own type and wanted to have `+` for it, and say an integer.

```julia
struct PlayerScore
    player_name::String
    score::Int
end

me = PlayerScore("Johnny", 22)
```

```julia
me += 10 #will generate an error since `+` for me and an integer is not defined and `+` is used in `+=`
```

So let's define it:

```julia
import Base: + #we do this to let Julia know we will add more methods to `+`

function +(ps::PlayerScore, n::Int)::PlayerScore
    return PlayerScore(ps.player_name, ps.score + n)
end

me += 10;
me
```

You can do this for every operation and function you want (and makes sense).  What if we wanted "pretty printing"?

```julia
import Base: show

show(io::IO, ps::PlayerScore) = print(io,"Score for $(ps.player_name) =  $(ps.score)")

println("We have a some score: $me. Pretty good!")
```

### Some examples...

Lets consider an example where we want to collect data and also have quick running statistics. E.g.

```julia
using Random, Statistics
Random.seed!(0)
get_new_data() = 100*rand() #This will in practice be anything...

data = Float64[]

function collect_and_use_data(data)
    for t in 1:100 #This will be much bigger 

        #Periodically we collect new data
        new_data_point = get_new_data()
        push!(data,new_data_point)

        #Peridoically we look at summary statistics
        if t%20 == 0
            println("-------")
            println("Mean: ", mean(data))
            println("Max: ", maximum(data))
        end
    end
end

collect_and_use_data(data)
```
Think of this scenario repeating often and for `t` being big... so you don't want to recompute the mean and max every time.

```julia
mutable struct RunningStatsData
    data::Vector{Float64}
    mean::Float64
    max::Float64
    RunningStatsData() = new([],NaN,NaN)
end

function show_summary(rsd::RunningStatsData)
        println("Mean: ", rsd.mean)
        println("Max: ", rsd.max)
end

data = RunningStatsData()
show_summary(data)
```

We can now make specifics method for `push!`, `max`, and `mean` for this new type:

```julia
Random.seed!(0)

import Base: push!, maximum
import Statistics: mean

maximum(rsd::RunningStatsData) = rsd.max
mean(rsd::RunningStatsData) = rsd.mean

function push!(rsd::RunningStatsData,data_point)
    #Insert the new datapoint
    push!(rsd.data, data_point)

    # Update the maximum
    if data_point > rsd.max || isnan(rsd.max)
        rsd.max = data_point
    end

    # Update the mean
    n = length(rsd.data)
    if n == 1 
        rsd.mean = data_point #If first data point just set it
    else
        rsd.mean = (1/n)*data_point + ((n-1)/n) * rsd.mean #if more than one data point then do running average.
    end
end

data = RunningStatsData()
collect_and_use_data(data)
```

Going a bit more generic we could have also had,

```julia
mutable struct FlexRunningStatsData{T <: Number}
    data::Vector{T}
    mean::Float64
    max::T
    FlexRunningStatsData{T}() where T = new([],NaN,typemin(T))
end

maximum(rsd::FlexRunningStatsData) = rsd.max
mean(rsd::FlexRunningStatsData) = rsd.mean

function push!(rsd::FlexRunningStatsData,data_point)
    #Insert the new datapoint
    push!(rsd.data, data_point)

    # Update the maximum
    if data_point > rsd.max || isnan(rsd.max)
        rsd.max = data_point
    end

    # Update the mean
    n = length(rsd.data)
    if n == 1 
        rsd.mean = data_point #If first data point just set it
    else
        rsd.mean = (1/n)*data_point + ((n-1)/n) * rsd.mean #if more than one data point then do running average.
    end
end

Random.seed!(0)
get_new_data() = rand(0:10^4) 

data = FlexRunningStatsData{Int}()
collect_and_use_data(data)
```

```julia
@which mean(data)
```

But there is a problem with the above. Should we have done `<: Number` or `<: Real`? What is `T` was `Complex`?

### Using structs for data structures

```julia
Random.seed!(0)

struct Node
    id::UInt16
    friends::Vector{Node}
    Node() = new(rand(UInt16), [])
    Node(friend::Node) = new(rand(UInt16),[friend])
end

"""
Makes 'n` children to node, each with a single friend
"""
function make_children(node::Node, n::Int, friend::Node)
    for _ in 1:n
        new_node = Node(friend)
        push!(node.friends, new_node)
    end
end

root = Node()
make_children(root, 3, root)
for node in root.friends
    make_children(node, 2,root)
end
root
```

### More to be covered as part of Unit 6:

[See constructors in Julia docs](https://docs.julialang.org/en/v1/manual/constructors/). More on this in Unit 6.

[See conversion and promotion in Julia docs](https://docs.julialang.org/en/v1/manual/conversion-and-promotion/)

[See interfaces in Julia docs](https://docs.julialang.org/en/v1/manual/interfaces/)